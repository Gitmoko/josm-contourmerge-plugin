apply plugin: "java"
apply plugin: "groovy"

/*
 * Add an entry for every published build. Increment the plugin version
 * and set the lowest JOSM version this plugin version is compatible with.
 */
project.ext.pluginVersions = [
    /* plugin version,  required JOSM version */
    [1009,              6317],
    [1008,              6317],
    [1007,              4394],
    [1005,              4394],
    [1004,              4394],
    [1003,              4394],
    [1000,              4223]
]

repositories {
    mavenCentral()
}

buildscript {
  repositories { mavenCentral() }
  dependencies { classpath 'org.ajoberstar:gradle-git:0.6.3' }
}

dependencies {
	compile files("contrib/josm-latest.jar")
	testCompile group: 'junit', name: 'junit', version: '4.+' 
	groovy localGroovy()
}

def currentPluginVersion() {
	return project.ext.pluginVersions.collect{it[0]}.max()
}

def currentPluginMainVersion() {
	return project.ext.pluginVersions.collect{it[1]}.max()
}

def bestPluginVersion(josmVersion) {
    return project.ext.pluginVersions
    	.findAll{it[1] == josmVersion}   
    	.collect{it[0]}
    	.max()
}

jar.baseName = "contourmerge"

jar {
	manifest {
		attributes(
			"Plugin-Date": new Date().format("yyyy-MM-dd HH:mm"),
			"Plugin-Version": currentPluginVersion(),
			"Plugin-Mainversion": currentPluginMainVersion(),
			'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')',
            'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
            'Plugin-Class' : "org.openstreetmap.josm.plugins.contourmerge.ContourMergePlugin",
			"Plugin-Description": "Merges the contours of two areas",
			"Plugin-Icon" : "images/mapmode/contourmerge.png",
			"Plugin-Link" : "http://wiki.openstreetmap.org/index.php/JOSM/Plugins/ContourMerge"
		)
		pluginVersions.collect{it[1]}.unique().sort().each {jv -> 
			def dv = bestPluginVersion(jv)
			def key = "${dv}_Plugin-Url".toString()
			def value = "${jv};https://raw.github.com/Gubaer/josm-contourmerge-plugin/for-josm-${dv}/dist/contourmerge.jar".toString()
			attributes([(key):value]) 
		}
	}
}

sourceSets {
    main {
        java.srcDirs = ["src"]
        output.classesDir = "build"
        resources {
            srcDir file(".")
        	include "README.md"
        	include "LICENSE"
        	include "images/**/*"
        	exclude "images/*.svg"
        	exclude "images/**/*.svg"
        }
    }
    test {
    	java.srcDirs = ["test"]
    	groovy.srcDirs = ["test"]
    	output.classesDir = "test/build"
    	resources {
    		srcDir file("test/config")
    	}
    }
}


test {
	doFirst {
		println "testClassesDir: " + testClassesDir
	}
	useJUnit()
	systemProperty "josm.home", file("test/josm.home")

	afterTest { desc, result -> 
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}


project.ext.josmUrl = "http://josm.openstreetmap.de/josm-latest.jar"
task  getJosm {
    description = "Download the latest JOSM build jar from the JOSM website"
	doLast {
		if (! file("contrib").exists()) {
			file("contrib").mkdir()
		}
		if (!file("contrib/josm-latest.jar").exists()) {	
			println "Downloading JOSM from <${project.ext.josmUrl}> ..."	
			def cmd ="wget ${project.ext.josmUrl} -O contrib/josm-latest.jar"
			cmd.execute().text
		}
	}
}
compileJava.dependsOn "getJosm"

import org.ajoberstar.gradle.git.tasks.*

tasks.create(name: "deploy.create-dist", type: GitAdd) {
	description = "Creates the 'dist' directory"
	doFirst {
		file("dist").mkdirs()
	}
	include "./dist"
}

tasks.create(name: "deploy.copy-to-dist", type: Copy) {
	description "Copies the jar to the local dist directory"
	from file("build/libs")
	into file("dist")
	include "contourmerge.jar"
}
tasks["deploy.copy-to-dist"].dependsOn "deploy.create-dist"

tasks.create(name: "deploy.commit-jar", type: GitCommit) {
    description = "Commits the jar"
    commitAll = true
    message = "commited plugin build ${currentPluginVersion()}"
    /*
    //Note: not using GitSubmit from the Git plugin, doesn't seem to work
    doLast {
    	def cmd = "git commit -m 'commited plugin build ${currentPluginVersion()}' dist/contourmerge.jar"
    	println cmd
    	def sout = new StringBuffer(), serr = new StringBuffer()
    	def proc = cmd.execute()
    	proc.consumeProcessOutput(sout, serr)
		proc.waitForOrKill(1000)
		println "out> $sout err> $serr"
    }
    */
}
tasks["deploy.commit-jar"].dependsOn "deploy.copy-to-dist"


tasks.create(name: "deploy.tag", type: GitTag) {
	description = "Creates a tag for the JOSM version this plugin is compatible with"
	force = true
	tagName = "for-josm-${currentPluginMainVersion()}"
}
tasks["deploy.tag"].dependsOn "deploy.commit-jar"


tasks.create(name: "deploy.push") {
	description = "Pushes the jar an the relevant tags to GitHub"
	doLast {
		println "Pushing current build ..."	
		def cmd ="git push origin deploy"
		cmd.execute().text		
		def tagName = "for-josm-${currentPluginMainVersion()}"
		println "Pushing tag $tagName ..."	
		cmd ="git push origin $tagName"
		cmd.execute().text		
	}
}
tasks["deploy.push"].dependsOn "deploy.tag"


task deploy
deploy.dependsOn "deploy.push"
