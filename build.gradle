plugins {
    // uses a local development fork of the gradle josm plugin
    id 'org.openstreetmap.josm' version '0.5.0-77-g30a855a-dirty'
    id 'java'
    id 'groovy'
    id 'eclipse'
}

repositories {
    mavenCentral()
}

dependencies {

    compileOnly "org.projectlombok:lombok:1.16.20"

    packIntoJar group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    testImplementation group: 'junit', name: 'junit', version: '4.+'
    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.13'
}

version="v0.1.0"
archivesBaseName = "contourmerge"

josm {
    josmCompileVersion = "latest"
    manifest {
        includeLinksToGithubReleases = true
        minJosmVersion = 12875

        description = 'Merges the contours of two areas'
        mainClass = 'org.openstreetmap.josm.plugins.contourmerge.ContourMergePlugin'
        iconPath = 'images/mapmode/contourmerge.png'
        website = new URL("http://wiki.openstreetmap.org/index.php/JOSM/Plugins/ContourMerge")
        canLoadAtRuntime = true
    }
}

compileJava {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

test {
    useJUnit()
    systemProperty "josm.home", file("src/test/resources/josm.home")

    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

sourceSets {
    main.resources {
        exclude "images/**/*.svg"
    }
}

processResources {
    from("$projectDir/README.md")
    from("$projectDir/LICENSE")
}

publishToGithubRelease {
    remoteJarName = "contourmerge.jar"
}